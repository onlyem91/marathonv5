/*
Copyright 2016 Jalian Systems Pvt. Ltd.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

repositories {
  mavenCentral()
  maven {
    url 'https://oss.sonatype.org/content/repositories/snapshots'
  }
}

configurations {
  antBuildXML
}

eclipse {
  jdt {
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'
    javaRuntimeName = 'JavaSE-1.6'

    file {
      withProperties { properties ->
        properties.setProperty('org.eclipse.jdt.core.compiler.problem.discouragedReference', 'ignore')
        properties.setProperty('org.eclipse.jdt.core.compiler.problem.forbiddenReference', 'ignore')
      }
    }
  }
}

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

compileJava {
	options.fork = true
	if(project.properties['jdk6home'])
		options.forkOptions*.executable = project.properties['jdk6home'] + '/bin/javac'
}

compileTestJava {
	options.fork = true
	if(project.properties['jdk6home'])
		options.forkOptions*.executable = project.properties['jdk6home'] + '/bin/javac'
}

dependencies {
  compile files('lib/jviews-chart-all-8.9.6.jar')
  compile files('lib/jviews-framework-all-8.9.jar')
  compile files('lib/vos-twd-gui-3.3.1-SNAPSHOT.jar')
  compile project(":marathon-core:JSON-java")
  compile "org.nanohttpd:nanohttpd:${nanohttpd_version}"
  compile "org.javassist:javassist:${javassist_version}"
  testCompile "org.testng:testng:${testng_version}"
  testCompile project(":marathon-java:marathon-test-helpers")
  antBuildXML project(":marathon-core:JSON-java").files("bin")
  antBuildXML "org.nanohttpd:nanohttpd:${nanohttpd_version}"
  antBuildXML "org.javassist:javassist:${javassist_version}"
}

jar {
  configurations.compile.each { from(zipTree(it.path).matching { exclude("META-INF/**") }) }
  manifest {
    attributes 'Manifest-Version': '1.0',
      'Agent-Class': 'net.sourceforge.marathon.javaagent.JavaAgentHook',
      'Can-Redefine-Classes': 'false',
      'Can-Retransform-Classes': 'false',
      'Premain-Class': 'net.sourceforge.marathon.javaagent.JavaAgentHook'
  }
}

test {
  useTestNG()
  systemProperty 'file.encoding', 'utf8'
  testLogging.showStandardStreams = true
}
